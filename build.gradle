// The buildscript part we include all our build plugins here

buildscript {

  // The latest "stable" kotlin version
  ext.kotlin_version = "0.14.451"

    // We specify the repositories that host the dependencies
    repositories {
      jcenter()
      mavenCentral()
    }

  /* The Dependencies:
   *  - Android Gradle plugin
   *  - Kotlin Gradle plugin
   *  - Kotlin Android Extensions
   */
  dependencies {
    classpath "com.android.tools.build:gradle:1.3.1"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
  }
}

// apply our plugins here
apply plugin:"com.android.application"
apply plugin:"kotlin-android"

// Global repositories
repositories {
  jcenter()
  mavenCentral()
  maven {
    url "http://repository.jetbrains.com/all" // Jetbrain's own maven
  }
}

// Android build settings
android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1" // check from your SDK Manager

  defaultConfig {
    applicationId "com.ykode.research.KotlinApp" // the AppID for Google Play
    minSdkVersion 21    // We only do Lollipop+
    targetSdkVersion 23 // Android M Sdk Build
    versionCode 1       // The build number
    versionName "1.0"   // The version used for marketing purposes
  }

  // make it compatible to Java 6
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
  }

  // progruard settings for release build
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  // Where we save the source code
  sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
  }
}

// Our app global dependencies
dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
